  we will understand For Loop for defining iteration techniques  in variables.tf file.

##### For Loop for List Variables

  In this Example we will define for loop for our Google Buckets and using technique to convert list to set of variables `toset`  and also using `each.key` method

  main.tf :

```
## BUCKETS
resource "google_storage_bucket" "environment_buckets" {
  for_each = toset(var.environment_list)
  name = "${lower(each.key)}_${var.project-id}"
  location = "US"
  versioning {
    enabled = true
  }
}
```

  variables.tf :
```
variable "environment_list" {
  type = list(string)
  default = ["DEV","QA","STAGE","PROD"]
}
```


##### For Loop For map Variables
 In this Example we have defined for loop for our google instance resource and using map variables in our variables.tf file , using `each.value` and `each.key` for them :

  main.tf :
```
## WEBSERVERS-MAP
resource "google_compute_instance" "web-map-instances" {
  for_each = var.environment_instance_settings
  name = "${lower(each.key)}-web"
  machine_type = each.value.machine_type
  labels = each.value.labels
  
  boot_disk {
    initialize_params {
      image = "debian-cloud/debian-11"
    }
  }
  network_interface {
    network = data.google_compute_network.default.self_link
    subnetwork = google_compute_subnetwork.subnet-1.self_link
  }  
}
```

  variables.tf :

```
variable "environment_instance_settings" {
  type = map(object({machine_type=string, labels=map(string)}))
  default = {
    "DEV" = {
      machine_type = "f1-micro"
      labels = {
        environment = "dev"
      }
    },
   "QA" = {
      machine_type = "e2-micro"
      labels = {
        environment = "qa"
      }
    },
    "STAGE" = {
      machine_type = "e2-micro"
      labels = {
        environment = "stage"
      }
    },
    "PROD" = {
      machine_type = "e2-medium"
      labels = {
        environment = "prod"
      }
    }
  }
}
```
